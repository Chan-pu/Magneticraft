buildscript {
    ext.name = 'Magneticraft_1.14'
    ext.project_kotlin_version = '1.3.50'
    ext.mod_version = '2.9.0'

    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$project_kotlin_version"
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'
apply plugin: 'kotlin'

archivesBaseName = name
version mod_version + '-' + getBuildNumber()
group 'com.cout970.magneticraft'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20190719-1.14.3'

    // Disabled:
    // makeObfSourceJar = false
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            mods {
                magneticraft {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                magneticraft {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'magneticraft', '--all', '--output', file('src/generated/resources/')

            mods {
                magneticraft {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

dependencies {
    // Minecraft and Forge
    minecraft 'net.minecraftforge:forge:1.14.4-28.1.61'

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$project_kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$project_kotlin_version"

    // ModelLoader
    compile files("lib/modelloader-2.0.0.jar")

    // JUnit for tests
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes([
            "Specification-Title": "Magneticraft",
            "Specification-Vendor": "MagneticraftTeam",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"MagneticraftTeam",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}


def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

task updateModVersion {
    doLast {
        exec {
            workingDir 'src/main/kotlin/com/cout970/magneticraft'
            commandLine 'sed', '-i', '-e', """s/version = "[1-9]*\\.[1-9]*\\.[1-9]*"/version = "${mod_version}"/g""", 'Magneticraft.kt'
        }
    }
}

compileKotlin.dependsOn updateModVersion

static String getBuildNumber() {
    String buildNumber = System.getenv("BUILD_NUMBER") != null ? System.getenv("BUILD_NUMBER") : ""
    if(buildNumber == "") {
        buildNumber = System.getenv("bamboo_buildNumber") != null ? "." + System.getenv("bamboo_buildNumber") : ""
    }
    if(buildNumber == null || buildNumber == ""){
        buildNumber = "dev"
    }
    return buildNumber
}
